Html {
  Fragment 
    = Node* end

  Node 
    = "<" tag Attribute* ">" Node* "</" tag? ">"  -- container
    | "<" tag Attribute* "/>"                     -- void
    | Text                                        -- text
    | JsExpr                                      -- dynamic

  Attribute
    = attr "=" Literal    -- literal
    | JsExpr              -- dynamic
  
  JsExpr = insertion_point
  Text = text
  Literal = string


  // ## Lexical structure
  newline = "\n" | "\r"
  line = (~newline any)*
  comment = "//" line
  space += comment

  id_start = letter
  id_rest = letter | digit
  id = id_start id_rest*

  tag = id
  attr = id

  string = "\"" string_character* "\""

  string_character
    = ~("\"" | "\\") any      -- regular
    | "\\" escape_sequence    -- escaped

  escape_sequence
    = "b"                   -- backspace
    | "f"                   -- formfeed
    | "n"                   -- newline
    | "r"                   -- return
    | "t"                   -- tab
    | "u" unicode_escape    -- unicode
    | "\""                  -- quote
    | "\\"                  -- backslash

  raw_hex_digit 
    = "0" .. "9" | "a" .. "f" | "A" .. "F"

  unicode_escape
    = raw_hex_digit raw_hex_digit raw_hex_digit raw_hex_digit

  insertion_point
    = "<$" digit+ ">"

  text_character
    = ~("<" | ">") any    -- regular
    | "\\" any            -- escaped

  text
    = text_character+
}